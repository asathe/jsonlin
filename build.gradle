group "org.github.asathe.jsonlin"
version "0.1"

buildscript {
    ext.kotlin_version = "1.0.3"
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "maven-publish"

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "junit:junit:4.12"
}

sourceSets {
    main.java.srcDirs += "src/main/kotlin"
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "jsonlin",
                "Implementation-Version": version
        )
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "jsonlin"

            from components.java

            artifact sourceJar
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}
